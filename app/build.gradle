apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        testInstrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'
        applicationId = 'com.unitwallet'
        minSdkVersion 23
        targetSdkVersion 26
        versionCode 1
        versionName "fp-1"
        multiDexEnabled true

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }

    flavorDimensions "mode"

    productFlavors {
        unit {
            applicationId "com.unitwallet"
            dimension "mode"
            resValue "string", "app_name", "Unit Wallet"
            buildConfigField "boolean", "BITCOIN_TESTNET", "false"
            buildConfigField "String", "CASHADDR_PREFIX", '"bitcoincash"'
            externalNativeBuild.cmake {
                cFlags "-DBITCOIN_TESTNET=0"
                targets "core"
            }
        }
        demo {
            applicationId "com.unitwallet"
            dimension "mode"
            resValue "string", "app_name", "Unit Wallet-Demo"
            buildConfigField "boolean", "BITCOIN_TESTNET", "true"
            buildConfigField "String", "CASHADDR_PREFIX", '"bchtest"'
            externalNativeBuild.cmake {
                cFlags "-DBITCOIN_TESTNET=1"
                targets "core"
            }
        }
    }

    lintOptions.with {
        // set to true to turn off analysis progress reporting by lint
        quiet = false
        explainIssues = true
        // if true, stop the gradle build if errors are found
        abortOnError = true
        // if true, only report errors
        ignoreWarnings = false

        disable 'MissingTranslation'
    }

    buildTypes {
        gitUpdateModules()
        release {
            proguardFiles.add(file('proguard-rules.pro'))
            minifyEnabled = false
            debuggable = false
            ndk {
                minifyEnabled = false
                debuggable = false
            }
        }
        debug {
            debuggable = true
            minifyEnabled = false
            ndk {
                minifyEnabled = false
                debuggable = true
            }
        }
    }
}

def gitUpdateModules() {
    //['sh', '-c', 'git submodule foreach git pull origin master'].execute().text.trim()
    //getLogger().debug("gitUpdateModules done!")
}

dependencies {
    compile 'commons-io:commons-io:2.0.1'
    compile 'org.eclipse.jetty:jetty-webapp:9.2.19.v20160908'
    compile 'org.eclipse.jetty.websocket:websocket-server:9.2.19.v20160908'
    compile 'org.eclipse.jetty:jetty-continuation:9.2.19.v20160908'
    compile 'org.slf4j:slf4j-api:1.7.22'
    //    compile 'org.apache.commons:commons-compress:1.8.1'
    //    compile group: 'org.apache.ant', name: 'ant', version: '1.8.2'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:gridlayout-v7:26.1.0'
    compile 'com.android.support:support-v13:26.1.0'
    compile 'com.google.android.gms:play-services-vision:11.8.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.google.firebase:firebase-core:11.8.0'
    compile 'com.google.firebase:firebase-crash:11.8.0'
    compile 'io.sigpipe:jbsdiff:1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.volley:volley:1.0.0'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}
apply plugin: 'com.google.gms.google-services'
